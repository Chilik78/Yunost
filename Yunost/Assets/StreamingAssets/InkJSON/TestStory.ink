INCLUDE globals.ink
EXTERNAL startMiniGame()
EXTERNAL setDoneTask()

{МетодПроникновения == "ОлегПомог": -> МетодПроникновенияОлегПомог}
{МетодПроникновения == "Таран": -> МетодПроникновенияТаран}
{МетодПроникновения == "Взлом": -> МетодПроникновенияВзлом}

-> main

=== main ===
Олег: Я тебя слушаю, друг. 
    + [Спать хочется. Почему ты ещё не зашёл в дом?] -> Проникновение
    + [Как у тебя настроение? (- Время)] -> Настроение
    + [Закончить разговор] -> END
    
=== ПослеЛичногоДиалога ===
    + [Спать хочется. Почему ты ещё не зашёл в дом?] -> Проникновение
    + [Закончить разговор] -> END
    
=== Проникновение ===
Вы: Спать хочется. Почему ты ещё не зашёл в дом?
Олег: Друг, ключ от твоего домика так и не смогли найти. Ты можешь попробовать открыть замок отмычкой самостоятельно. А на самый крайний случай...выломать замок силой...Как поступишь?
    ~ setDoneTask()
    + [Можешь мне помочь со взломом? (Лояльность)]
        -> ВзломОлега
    + [Я попробую взломать замок.]
        -> Взлом
    + [Выбью дверь. (- Здоровье)] 
        -> Таран

== ВзломОлега ==
~ МетодПроникновения = "ОлегПомог"
{ПомощьОлега=="Да":
Вы: Можешь помочь мне со взломом? (Лояльность - Успех)
Олег: Могу, только найди мне для этого подходящие инструменты. Можешь поискать за домом. Мне нужен набор взломщика. Можешь найти отвёртку и отмычку и совместить их.  
-> Конец
- else:
Вы: Можешь помочь мне со взломом? (Лояльность - Неудача)
Олег: А знаешь, раз уж ты так целеустремлён заниматься полезными делами, то и опыт взлома лишним для тебя не будет. Пробуй открыть дверь сам.
    + [Я попробую взломать замок.] -> Взлом
    + [Выбью дверь. (- Здоровье)] -> Таран

}

== Взлом ==
~ МетодПроникновения = "Взлом"
Вы: Давай попробуем взломать замок. Я думаю, никто точно не заметит. 
Олег: А ты оптимист. Держи отмычки. Я понаблюдаю, чтобы нас не заметили.
    + Начинаю взламывать 
    // Функция запуска миниигры
    ~startMiniGame()
    ~setDoneTask()
    -> END

== Таран ==
~ МетодПроникновения = "Таран"
Вы: Слушай, а давай просто выбьем эту дверь. Сэкономим время, а то в сон клонит сильно.
Олег: Ну ты зверь, конечно! Смотри не повреди плечо, а я пока постою, понаблюдаю.
-> Конец

=== Настроение ===
Вы: Как у тебя дела? Гляжу, ты какой-то слишком уж задумчивый.
Олег: Да а как тут не задуматься, моргнуть и не успел, а нас уже настигла последняя лагерная смена. Да и осенью дороги наши разойдутся. Новый этап жизни всё-таки и неизвестно как всё сложится.
    +[Да не парься, Олег! Получай удовольствие от момента, а осенью не пожалеешь.] -> ПоддержалОлега
    +[Олег, о глупостях каких-то ты думаешь. У нас есть на данный момент более насущные задачи.] -> НеПоддержалОлега

=== ПоддержалОлега ===
Вы: Да не парься, Олег! Получай удовольствие от момента, а осенью не пожалеешь.
Олег: Да...наверное ты прав. Что-то я слишком рано стал мозги этим забивать. Спасибо тебе за поддержку.
~ ПомощьОлега = "Да"
-> ПослеЛичногоДиалога


=== НеПоддержалОлега ===
Вы: Олег, о глупостях каких-то ты думаешь. У нас есть на данный момент более насущные задачи.
Олег: Я не считаю это глупостями. Но толку с тобой это обсуждать...Проехали.
~ ПомощьОлега = "Нет"
-> ПослеЛичногоДиалога

=== Конец ===
    +[За дело!]
        -> END

=== МетодПроникновенияВзлом ===
Олег: Поздравляю, у тебя получилось! Да и дверной замок не пострадал. Иди отдыхать, встретимся позже.
    + Спасибо за помощь. Надеюсь, я проснусь не скоро. -> END
    
=== МетодПроникновенияОлегПомог ===
Олег: Спасибо за инструменты. Сейчас помогу тебе открыть дверь.
    + [Спасибо за помощь!] 
    ~setDoneTask()
    -> END
    
=== МетодПроникновенияТаран ===
Олег: Дерзай! Я бы тебе помог выбить дверь, но плечо потянул. Подстрахую со стороны.
    + [Закончить разговор] -> END
